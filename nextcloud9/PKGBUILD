developer=http://owncloud.org/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=9.0.54
pkgrel=2
pkgdesc="A safe home for all your data"
arch=('any')
license=("AGPL")
source=("https://download.nextcloud.com/server/releases/nextcloud-${pkgver}.tar.bz2")
depends=(
    'php'
    'smtp-server')
options=('!strip')
sha512sums=('32f079f814d209f36445c1cc5120b461968ff5d9634cee57d35d114154b41afca083cfec120fcc32d5437c180dbc1a8cc667ca5673099a043ee7fcaf1d8fd99d')

prepare() {
    patch ${startdir}/src/nextcloud/lib/private/security/trusteddomainhelper.php ${startdir}/allow-wildcard-trusted.patch
}

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/$pkgname
    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/$pkgname/

# Data
    mkdir -p ${pkgdir}/var/lib/nextcloud9
    
# Code
    mkdir -p ${pkgdir}/usr/share/${pkgname}/bin
    install -m755 ${startdir}/bin/{create-ocdata,fix-permissions,fix-restore,generate-autoconfig,upgrade,web-install}.pl ${pkgdir}/usr/share/${pkgname}/bin/

    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    install -m644 ${startdir}/tmpl/htaccess.tmpl ${pkgdir}/usr/share/${pkgname}/tmpl/
    touch ${pkgdir}/usr/share/${pkgname}/tmpl/ocdata

    cp -dr --no-preserve=ownership ${startdir}/src/nextcloud ${pkgdir}/usr/share/${pkgname}/
}
