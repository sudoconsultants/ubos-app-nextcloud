developer=https://nextcloud.com/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=17.0.8
pkgrel=1
pkgdesc="A safe home for all your data"
arch=('any')
license=("AGPL3")
source=("https://download.nextcloud.com/server/releases/nextcloud-${pkgver}.tar.bz2")
releasepage=('https://nextcloud.com/changelog/')
depends=(
    'php'
    'php-systemd'
    'smtp-server')
options=('!strip')
sha512sums=('c9a711fb1ad6871233fea581d6cbfe7083e925683235a86023842d3c9d65db6779218bce06c5960743fb277bb779c6cad4eb40eca65ade9e50f8459cbee531c3')

prepare() {
    # Patches to make work with PHP 7.4
    ( cd ${srcdir}/nextcloud/lib                              ; patch < ../../../versioncheck.patch )
    ( cd ${srcdir}/nextcloud/lib/private/Log                  ; patch < ../../../../../exception-serializer.patch )
    ( cd ${srcdir}/nextcloud/lib/private/AppFramework/Utility ; patch < ../../../../../../reflectiontype.patch )
    ( cd ${srcdir}/nextcloud/3rdparty/leafo/scssphp/src       ; patch < ../../../../../../compiler.patch )
}

package() {
# Manifest
    install -D -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m0644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Systemd
    install -D -m0644 ${startdir}/systemd/nextcloud17@.timer -t ${pkgdir}/etc/systemd/system/

# Data
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Code
    install -D -m0755 ${startdir}/bin/*.pl -t ${pkgdir}/ubos/share/${pkgname}/bin/

    install -D -m0644 ${startdir}/tmpl/*.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

    cp -dr --no-preserve=ownership ${startdir}/src/nextcloud ${pkgdir}/ubos/share/${pkgname}/
}
